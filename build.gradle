buildscript {
	repositories {
		mavenCentral()
		maven { url 'https://s01.oss.sonatype.org' }
		mavenLocal()
		google()
		gradlePluginPortal()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
		maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
		// add jitpack repo
		maven { url 'https://jitpack.io' }
	}
	dependencies {

		classpath group: 'org.mini2Dx', name: 'butler', version: '2.1.0'
		classpath "org.wisepersist:gwt-gradle-plugin:$gwtPluginVersion"
		classpath "com.badlogicgames.gdx:gdx-tools:$gdxVersion"


		// This follows advice from https://blog.gradle.org/log4j-vulnerability
		constraints {
			classpath("org.apache.logging.log4j:log4j-core") {
				version {
					strictly("[2.17, 3[")
					prefer("2.17.0")
				}
				because("CVE-2021-44228, CVE-2021-45046, CVE-2021-45105: Log4j vulnerable to remote code execution and other critical security vulnerabilities")
			}
		}
	}
}


// Store the parameters you want to pass the texturePacker here...
project.ext.texturePacker = [ "assets/input/path/", "assets/output/path/", "atlas_name" ]

// Import the texture packer
import com.badlogic.gdx.tools.texturepacker.TexturePacker

// Add a new task that packs the textures for you
task texturePacker {
	doLast {
		if (project.ext.has('texturePacker')) {
			logger.info "Calling TexturePacker: "+texturePacker
			TexturePacker.process(texturePacker[0], texturePacker[1], texturePacker[2])
		}
	}
}


allprojects {
	apply plugin: 'eclipse'
	apply plugin: 'idea'
}

configure(subprojects) {
	apply plugin: 'java-library'
	sourceCompatibility = 8
	compileJava {
		options.incremental = true
	}
	dependencies {
		// This follows advice from https://blog.gradle.org/log4j-vulnerability
		constraints {
			implementation("org.apache.logging.log4j:log4j-core") {
				version {
					strictly("[2.17, 3[")
					prefer("2.17.0")
				}
				because("CVE-2021-44228, CVE-2021-45046, CVE-2021-45105: Log4j vulnerable to remote code execution and other critical security vulnerabilities")
			}
		}
	}

}

subprojects {
	version = '0.0.2'
	ext.appName = 'UniversalEmpire'
	repositories {
		mavenCentral()
		maven { url 'https://s01.oss.sonatype.org' }
		mavenLocal()
		gradlePluginPortal()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
		maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
		maven { url 'https://jitpack.io' }
	}
}

project(":html") {
	apply plugin: "org.mini2Dx.butler"

	butler {
		user = "hirshi001"
		game = "universalempire"
		allChannelsPostfix = "-beta"
	}

	task butlerPush(type: org.mini2Dx.butler.task.PushTask) {
		dependsOn("dist")
		binDirectory = new File("${project.rootDir}/html/build/dist");
		channel = "html"
	}
}

eclipse.project.name = 'UniversalEmpire' + '-parent'
